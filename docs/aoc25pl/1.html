<!DOCTYPE html>
<html>
<head>
  <title>manzikert.ca</title>
  <style>
body     { display:flex; flex-direction:column; align-items:center;
           font-size:1rem; margin:min(10vw,100px) auto; }
pre      { max-width:min(80ch,80vw); margin:auto; white-space:pre-wrap; }
.code    { max-width:80vw; word-break:break-all; }
.white   { color:#444444; }
.red     { color:#ff0000; }
.green   { color:#00aa00; }
.hgreen  { color:#008800; }
.blue    { color:#0000ff; }
.yellow  { color:#ddaa00; }
.magenta { color:#ff00ff; }
.cyan    { color:#00cccc; }
  </style>
</head>
<body><div>
<pre class='code'>
state(S0, S), [S] --&gt; [S0].

number(0) --&gt; [].
number(N) --&gt; number(Ns), [Dig], { number_chars(X, [Dig]), N #= Ns*10 + X }.

instruction((right, N)) --&gt; "R", number(N).
instruction((left,  N)) --&gt; "L", number(N_), { N #= -N_ }.

lines([])     --&gt; [].
lines([L|Ls]) --&gt; instruction(L), "\n", lines(Ls).

input(I) :- phrase_from_file(lines(I), "i/1.in").

box(right, N, Box) :- Box #= N div 100.
box(left,  N, Box) :- N_ #= N - 1, Box #= N_ div 100.

turn((Dir, N)) --&gt; state((Dial, P1, P2), (Dial_, P1_, P2_)),
		 { Dial_ #= Dial + N,
		   ( 0 #= Dial_ mod 100 -&gt; P1_ #= P1 + 1 ; P1_ #= P1),
		   box(Dir, Dial, Box1), box(Dir, Dial_, Box2), P2_ #= P2 + abs(Box1 - Box2) }.

execute([])     --&gt; state(N, N).
execute([I|Is]) --&gt; turn(I), execute(Is).

main :- input(I), phrase(execute(I), [(50, 0, 0)], [(_, P1, P2)]), write(P1), nl, write(P2), halt.
:- initialization(main).
</pre><pre>

Well, this was certainly harder than your average day 1. I use a DCG to parse the input, then use another to simulate turning the dial and keeping track of zeros.

For part two, I imagine that each multiple of 100 is in its own "box". For example, -5 is in box -1, and 123 is in box 1. If you turn from -5 to 123, you cross from box -1 -&gt; box 0 -&gt; box 1, so you pass over two 0s. The only tricky thing is that you need to define boxes differently for turning left/right to get this to work.
</pre>
<br>
<pre style='width:100%;text-align:center'><br><a href='/'>~</a>/<a href='./'>aoc25pl</a>/<a href='1'>1</a></pre>
</div></body>
</html>
