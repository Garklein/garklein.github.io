<!DOCTYPE html>
<html>
<head>
  <title>manzikert.ca</title>
  <style>
body     { display:flex; flex-direction:column; align-items:center;
           font-size:1rem; margin:min(10vw,100px) auto; }
pre      { max-width:min(80ch,80vw); margin:auto; white-space:pre-wrap; }
.code    { max-width:80vw; word-break:break-all; }
.white   { color:#444444; }
.red     { color:#ff0000; }
.green   { color:#00aa00; }
.hgreen  { color:#008800; }
.blue    { color:#0000ff; }
.yellow  { color:#ddaa00; }
.magenta { color:#ff00ff; }
.cyan    { color:#00cccc; }
  </style>
</head>
<body><div>
<pre class='code'>
clpz:monotonic.

space(' ')   --&gt; " ".
opCol([C|S]) --&gt; [C], { member(C,"+*") }, sequence(space,"",S).
parse_line(Line,Parsed)  :- phrase(sequence(seq," ",Parsed),Line).
parse_spaces_number(S,N) :- phrase((sequence(space,"", _),number(N),sequence(space,"",_)),S).

input(Ops,Cols) :- phrase_from_file((sequence(seq,"\n",Lines),"\n",sequence(opCol," ",OpCols)),"i/06.in"),
	maplist(nth0(0),OpCols,Ops), maplist(maplist(same_length,OpCols),LinesOfNums),
	maplist(parse_line,Lines,LinesOfNums), transpose(LinesOfNums,Cols).

do_op('+')         --&gt; sum.
do_op('*',Xs,Prod) :- foldl(\X^Y^Z^(#Z #= #X * #Y),Xs,1,Prod).

part(Ops)    --&gt; maplist(maplist(parse_spaces_number)), maplist(do_op,Ops), sum.
solve(P1,P2) :- input(Ops,Cols), maplist(transpose,Cols,ColsT), part(Ops,Cols,P1), part(Ops,ColsT,P2).
test         :- make_test(day(6),solve,6891729672676,9770311947567).
</pre><pre>


Another fairly easy day. I'm especially happy with the maplist(maplist(same_length, OpCols), LineNums), which lines up all the columns for me. Also, there is secret tacit programming in Prolog with (--&gt;)/2 !! So excited to have figured that out.
</pre>
<br>
<pre style='width:100%;text-align:center'><br><a href='/'>~</a>/<a href='./'>aoc25pl</a>/<a href='06'>06</a></pre>
</div></body>
</html>
